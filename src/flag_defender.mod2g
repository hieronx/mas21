module flag_defender[exit=nogoals] {
	knowledge {
		% true if an item is within 1500 of the base
		itemInRange(Position) :- navPoint(Position, PointLoc, _), ownTeam(Team), base(Team, BasePos), manhattanDistance(PointLoc, BasePos, Distance), Distance > 1500.
	}
  	program {
		if bel( not(role(flag_defender)) ) 
				then exit-module. 
					
		% If the bot sees any dropped flag, or the enemy flag in their base go to module seeFlag
		if bel( seeDroppedFlag )
			  then seeFlag.
			  
		% if adrenaline is 100, there is a usefull combo and this is a good timing for that combo
		if bel(status(_, _, 100, _), usefullCombo(Combo), goodComboTiming(Combo))
			then combo(Combo).
		
		if bel( isStuck )
			then respawn.
	  
		if bel( not( hasGoodWeapon ) )
			then getWeapon.
			
		% if there's a goal to get an item and the distance from the base towards the item is < 500 , check if it's still valid and then go to it
		if goal( getItem(ID, Label, ItemType, Position) )
			then {
				% don't go to the item if we don't need it anymore
				if bel( not(usefulItem(Label, ItemType)) )
					then drop(getItem(ID, Label, ItemType, Position)).

				% don't go to the item if there's no item anymore
				if bel( not(item(ID, Label, ItemType, Position)) )
					then drop(getItem(ID, Label, ItemType, Position)).

				% if the item is too far away, don't go to it
				if bel( not(itemInRange(Position)) )
					then drop(getItem(ID, Label,ItemType, Position)).
				
				% otherwise, go to the item
				if bel( not(navigation(navigating, Position)) )
					then navigate(Position).
		}
			
		 %if the distance towards the homebase is bigger than 1500, return to homebase
		if bel(ownTeam(Team), flagState(Team, home), base(Team, BasePos), distance(BasePos, Distance), Distance > 1500 )
				then navigate(BasePos).		
		
		% if there is an item and it's in range and it's useful
		if not(goal(getItem(_, _, _, _))), bel(item(UnrealID, Label, ItemType, Position), itemInRange(Position), usefulItem(Label, ItemType))
			then adopt( getItem(UnrealID, Label, ItemType, Position) ).

		if not(goal(getItem(_, _, _, _))), bel(ownTeam(Team), base(Team, BasePos), not(navigation(navigating, BasePos)))
			then navigate(BasePos).
  }
}
